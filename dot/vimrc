" ============================================================================
" Vim-plug initialization

" Create the autocmd group used by all my autocmds (cleared when sourcing vimrc)
augroup vimrc
  autocmd!
augroup END

" This has to be before everything else because the rest only works if vim-plug
" is installed
if empty(glob('~/.vim/autoload/plug.vim'))
  silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
        \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
  autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

" ============================================================================
" Active plugins

" this needs to be here, so vim-plug knows we are declaring the plugins we
" want to use
call plug#begin('~/.local/share/nvim/plugged')
filetype plugin on
filetype plugin indent on

" Features
Plug 'neoclide/coc.nvim', {'branch': 'release'}                   " coc.nvim autocomplete
Plug 'dense-analysis/ale'                                         " Code linter
Plug 'tiagofumo/vim-nerdtree-syntax-highlight'                    " Syntax highlighting file tree
Plug 'scrooloose/nerdtree'                                        " File tree browser
Plug 'Xuyuanp/nerdtree-git-plugin'                                " Git for NerdTree
Plug 'jistr/vim-nerdtree-tabs'                                    " NerdTree independent of tabs
Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all' } " Install fzf for user
Plug 'junegunn/fzf.vim'                                           " Fzf vim plugin
Plug 'antoinemadec/coc-fzf'
Plug 'preservim/tagbar'                                          " Tag bar

" Small utilities
Plug 'christoomey/vim-tmux-navigator'                             " Naviagte between tmux seamlessly
Plug 'tmux-plugins/vim-tmux-focus-events'                         " Enable tmux focused events
Plug 'kopischke/vim-fetch'

" Efficiency
Plug 'junegunn/vim-peekaboo'                                      " Register extender
Plug 'easymotion/vim-easymotion'
Plug 'haya14busa/incsearch.vim'
Plug 'haya14busa/incsearch-easymotion.vim'

" Languages
Plug 'sheerun/vim-polyglot'                                       " Better language pack
Plug 'fatih/vim-go', { 'do': ':GoUpdateBinaries'  }               " Syntax for go
Plug 'hashivim/vim-terraform'                                     " Syntax for terraform
Plug 'ekalinin/Dockerfile.vim'                                    " Syntax for docker
Plug 'vim-ruby/vim-ruby'
Plug 'tpope/vim-rails'

" tpope
Plug 'tpope/vim-surround'                                         " Operate on surrounding
Plug 'tpope/vim-repeat'                                           " Repeat plugins
Plug 'tpope/vim-commentary'                                       " Comment out blocks
Plug 'tpope/vim-fugitive'                                         " Git integration
Plug 'tpope/vim-rhubarb'                                          " Enables fugitive to open Git URLs
Plug 'tpope/vim-abolish'                                          " Flexible search

" Appearance
Plug 'joshdick/onedark.vim'                                       " Onedark color scheme
Plug 'ryanoasis/vim-devicons'                                     " Better icon packs
Plug 'vim-airline/vim-airline'                                    " Airline
Plug 'vim-airline/vim-airline-themes'                             " Airline themes
Plug 'Yggdroot/indentLine'                                        " Show indented lines

" Writing mode
Plug 'junegunn/goyo.vim'
Plug 'junegunn/limelight.vim'
Plug 'junegunn/seoul256.vim'
Plug 'rhysd/vim-grammarous'

call plug#end()

" ============================================================================
" Install missing plugins automatically on startup

autocmd vimrc VimEnter *
      \  if len(filter(values(g:plugs), '!isdirectory(v:val.dir)'))
      \|   PlugInstall --sync | q | runtime vimrc
      \| endif

" ============================================================================
" Bootstrap configuration

" global variables to enable plugins
syntax on
syntax enable
set nocompatible

" enable mouse
set mouse=a

" use system clipboard
set clipboard=unnamed
set clipboard+=unnamedplus

" search settings
set incsearch
set smartcase
set ignorecase
set nohlsearch

" line numbers
set number
set relativenumber
set cursorline

" very basic editor behaviour
set expandtab " tabs -> spaces
set tabstop=2
set wrap
set linebreak
set list lcs=tab:\▏\ " add indent line for tabs
set formatoptions+=j

" bash like tab completion
set wildmode=longest,list,full
set wildmenu
set wildignore+=*.pyc,*.o,*.obj,*.svn,*.swp,*.class,*.hg,*.DS_Store,*.min.*,*.git,__pycache__

" nuke all trailing space before a write
au BufWritePre * :%s/\s\+$//e

" put cursor back where it was last time when re-opening a file
au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif

" set dash as a word boundary
set iskeyword-=-

" Undo

" permanent undo history of files
set undodir=$HOME/.vim/undo
set undofile

" Automatically create directory for undo if it does not exist
if !isdirectory(expand('~').'/.vim/undo')
  !mkdir -p $HOME/.vim/undo
endif

" Swap

" enable swapfile
set swapfile
set directory=$HOME/.vim/swap

" Automatically create directory for swapfiles if it does not exist
if !isdirectory(expand('~').'/.vim/swap')
  !mkdir -p $HOME/.vim/swap
endif

" fuzzy completion
set runtimepath+=/.fzf

" set utf8 encoding by default
set encoding=utf8

" set some tab highlighting
set showtabline=2
set tabpagemax=500
highlight TabLine       ctermfg=green ctermbg=None cterm=None
highlight TabLineFill   ctermfg=white ctermbg=None cterm=None
highlight TabLineSel    ctermfg=202

" ============================================================================
" Shorctuts & key bindings

if !has('nvim')
  set ttymouse=xterm2
  " make redrawing smoother
  set ttyfast
endif

" Time out mapping after 100ms
" Shorten default to time to update swap files and gutter plugins
"set ttimeout
set timeoutlen=550
set ttimeoutlen=0

" Remap leader key
let mapleader = "\<Space>"

" quit/exit shortcuts fat fingers
cabbrev Q q
cabbrev Q! q!
cabbrev Q1 q!
cabbrev q1 q!
cabbrev Wq wq
map Y y$
map Q :q<CR>

" save shortcuts
map <C-s> :w!<CR>
imap <C-s> <ESC>:w!<CR>

" fzf
let g:fzf_action = {
  \ 'ctrl-t': 'tab split',
  \ 'ctrl-x': 'split',
  \ 'ctrl-v': 'vsplit' }

let g:fzf_layout = { 'down': '~20%' }

" Take colours from colour scheme
let g:fzf_colors = {
  \ 'fg':      ['fg', 'Normal'],
  \ 'bg':      ['bg', 'Normal'],
  \ 'hl':      ['fg', 'Comment'],
  \ 'fg+':     ['fg', 'CursorLine', 'CursorColumn', 'Normal'],
  \ 'bg+':     ['bg', 'CursorLine', 'CursorColumn'],
  \ 'hl+':     ['fg', 'Statement'],
  \ 'info':    ['fg', 'PreProc'],
  \ 'border':  ['fg', 'Ignore'],
  \ 'prompt':  ['fg', 'Conditional'],
  \ 'pointer': ['fg', 'Exception'],
  \ 'marker':  ['fg', 'Keyword'],
  \ 'spinner': ['fg', 'Label'],
  \ 'header':  ['fg', 'Comment'] }

" Hide statusline and mode indicator in fzf
autocmd! FileType fzf
autocmd FileType fzf set laststatus=0 noshowmode noruler
  \| autocmd BufLeave <buffer> set laststatus=2 showmode ruler

let g:fzf_nvim_statusline = 0 " disable statusline overwriting

" Smart shortcuts
nnoremap <silent> <leader><space> :Files<CR>
nnoremap <silent> <leader>a :Buffers<CR>
nnoremap <silent> <leader>A :Windows<CR>
nnoremap <silent> <leader>; :BLines<CR>
nnoremap <silent> <leader>. :Lines<CR>
nnoremap <silent> <leader>o :BTags<CR>
nnoremap <silent> <leader>O :Tags<CR>
nnoremap <silent> <leader>? :History<CR>
nnoremap <silent> <leader>/ :execute 'Rg ' . input('Rg/')<CR>

nnoremap <silent> K :call SearchWordWithRg()<CR>
vnoremap <silent> K :call SearchVisualSelectionWithRg()<CR>
nnoremap <silent> <leader>gl :Commits<CR>
nnoremap <silent> <leader>ga :BCommits<CR>
nnoremap <silent> <leader>ft :Filetypes<CR>

imap <C-x><C-f> <plug>(fzf-complete-file-ag)
imap <C-x><C-l> <plug>(fzf-complete-line)

" Use ripgrep to search for strings
function! SearchWordWithRg()
  execute 'Rg' expand('<cword>')
endfunction

function! SearchVisualSelectionWithRg() range
  let old_reg = getreg('"')
  let old_regtype = getregtype('"')
  let old_clipboard = &clipboard
  set clipboard&
  normal! ""gvy
  let selection = getreg('"')
  call setreg('"', old_reg, old_regtype)
  let &clipboard = old_clipboard
  execute 'Rg' selection
endfunction

" Paste below line
nmap <F4> o<ESC>p

" Grammar checker
nmap <F5> :GrammarousCheck<return>

" swap semi-colon for cmd mode
nmap ; :

" This allows you to visually select a section and then hit @ to run a macro
" on all lines.
xnoremap @ :<C-u>call ExecuteMacroOverVisualRange()<CR>

function! ExecuteMacroOverVisualRange()
  echo "@".getcmdline()
  execute ":'<,'>normal @".nr2char(getchar())
endfunction

" ============================================================================
" Tmux

" tmux and vim combination
if &term =~# '^screen'
    " tmux will send xterm-style keys when its xterm-keys option is on
    execute "set <xUp>=\e[1;*A"
    execute "set <xDown>=\e[1;*B"
    execute "set <xRight>=\e[1;*C"
    execute "set <xLeft>=\e[1;*D"
endif

" autocomplete
if exists('$TMUX')
    " Tmux completion (with tmux-complete plugin)
    let g:tmuxcomplete#trigger = ''
endif

" Pane switching
let g:tmux_navigator_no_mappings = 1
nnoremap <silent> <M-h> :TmuxNavigateLeft<cr>
nnoremap <silent> <M-j> :TmuxNavigateDown<cr>
nnoremap <silent> <M-k> :TmuxNavigateUp<cr>
nnoremap <silent> <M-l> :TmuxNavigateRight<cr>
nnoremap <silent> <M-\> :TmuxNavigatePrevious<cr>

" ============================================================================
" Colours and themes

if (has('termguicolors'))
  set termguicolors
endif

" Enable 256 colours
set background=dark
set t_Co=256

" Color scheme
colorscheme onedark

" Highlight on 80 characters
set colorcolumn=80

" ============================================================================
" Interface

" NERDTree ----------------------------------
syn match NERDTreeTxtFile #^\s\+.*txt$#
highlight NERDTreeTxtFile ctermbg=red ctermfg=magenta
let NERDTreeRespectWildIgnore=1

" Show hidden files
let NERDTreeShowHidden=1

" NERDTreeTabs configuration
map <Leader>n <plug>NERDTreeTabsToggle<CR>
let g:nerdtree_tabs_open_on_console_startup = 2

" Disable editor mode in default bar
set noshowmode

" Detect file encoding
if has('multi_byte')
  if &termencoding ==# ''
    let &termencoding = &encoding
    endif
  set encoding=utf-8
  setglobal fileencoding=utf-8
  setglobal bomb
  set fileencodings=ucs-bom,utf-8,latin1
endif

" Airline -----------------------------
let g:airline_powerline_fonts = 1
let g:airline#extensions#tabline#enabled = 1
let g:airline#extensions#tabline#tab_nr_type = 1
let g:airline#extensions#tabline#show_buffers = 0
let g:airline#extensions#tabline#show_splits = 0
let g:airline#extensions#tabline#formatter = 'unique_tail'
let g:webdevicons_enable_airline_statusline = 1
let g:webdevicons_enable_airline_tabline = 1
let g:WebDevIconsNerdTreeAfterGlyphPadding = '  '

" Indent line symbols -----------------
let g:indentLine_enabled = 1
let g:indentLine_char = '▏'

" Move modified -----------------------
let g:move_key_modifier = 'A-S'

" Vim easymotion -----------------------
function! s:incsearch_config(...) abort
  return incsearch#util#deepextend(deepcopy({
  \   'modules': [incsearch#config#easymotion#module({'overwin': 1})],
  \   'keymap': {
  \     "\<CR>": '<Over>(easymotion)'
  \   },
  \   'is_expr': 0
  \ }), get(a:, 1, {}))
endfunction

map , <Plug>(easymotion-prefix)

" <Leader>f{char} to move to {char}
map  <Leader>f <Plug>(easymotion-bd-f)
nmap <Leader>f <Plug>(easymotion-overwin-f)

" s{char}{char} to move to {char}{char}
nmap s <Plug>(easymotion-overwin-f2)

" Move to line
map <Leader>L <Plug>(easymotion-bd-jk)
nmap <Leader>L <Plug>(easymotion-overwin-line)

" Move to word
map  <Leader>w <Plug>(easymotion-bd-w)
nmap <Leader>w <Plug>(easymotion-overwin-w)

noremap <silent><expr> /  incsearch#go(<SID>incsearch_config())
noremap <silent><expr> ?  incsearch#go(<SID>incsearch_config({'command': '?'}))
noremap <silent><expr> g/ incsearch#go(<SID>incsearch_config({'is_stay': 1}))

" Goyo
autocmd! User GoyoEnter Limelight
autocmd! User GoyoLeave Limelight!

" Limelight
let g:limelight_paragraph_span = 1  " Don't dim one par around the current one
let g:limelight_priority       = -1 " Don't overrule hlsearch

" ============================================================================
" Markdown -------------------------------
let g:vim_markdown_conceal = 0
let g:vim_markdown_conceal_code_blocks = 0

" Coc-vim --------------------------------
let g:coc_global_extensions = [
      \ 'coc-fzf-preview',
      \ 'coc-git',
      \ 'coc-go',
      \ 'coc-json',
      \ 'coc-markdownlint',
      \ 'coc-pairs',
      \ 'coc-sh',
      \ 'coc-snippets',
      \ 'coc-solargraph',
      \ 'coc-vimlsp',
      \ 'coc-yaml',
      \ 'coc-yank',
      \ ]

" if hidden is not set, TextEdit might fail.
set hidden

" Better display for messages
set cmdheight=2

" Smaller updatetime for CursorHold & CursorHoldI
set updatetime=300

" don't give |ins-completion-menu| messages.
set shortmess+=c

" always show signcolumns
set signcolumn=yes

" Some servers have issues with backup files, see #649.
set nobackup
set nowritebackup

" Use tab for trigger completion with characters ahead and navigate.
" NOTE: Use command ':verbose imap <tab>' to make sure tab is not mapped by
" other plugin before putting this into your config.
inoremap <silent><expr> <TAB>
      \ pumvisible() ? "\<C-n>" :
      \ <SID>check_back_space() ? "\<TAB>" :
      \ coc#refresh()
inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<C-h>"

function! s:check_back_space() abort
  let col = col('.') - 1
  return !col || getline('.')[col - 1]  =~# '\s'
endfunction

" Use <c-space> to trigger completion.
inoremap <silent><expr> <c-space> coc#refresh()

" Make <CR> auto-select the first completion item and notify coc.nvim to
" format on enter, <cr> could be remapped by other vim plugin
inoremap <silent><expr> <cr> pumvisible() ? coc#_select_confirm()
                              \: "\<C-g>u\<CR>\<c-r>=coc#on_enter()\<CR>"

" Use K to show documentation in preview window.
"nnoremap <silent> L :call <SID>show_documentation()<CR>

function! s:show_documentation()
  if (index(['vim','help'], &filetype) >= 0)
    execute 'h '.expand('<cword>')
  elseif (coc#rpc#ready())
    call CocActionAsync('doHover')
  else
    execute '!' . &keywordprg . " " . expand('<cword>')
  endif
endfunction

" Highlight the symbol and its references when holding the cursor.
autocmd CursorHold * silent call CocActionAsync('highlight')

augroup mygroup
  autocmd!
  " Setup formatexpr specified filetype(s).
  autocmd FileType typescript,json setl formatexpr=CocAction('formatSelected')
  " Update signature help on jump placeholder.
  autocmd User CocJumpPlaceholder call CocActionAsync('showSignatureHelp')
augroup end

" Symbol renaming.
nmap <leader>rn <Plug>(coc-rename)

" Formatting selected code.
xmap <leader>f  <Plug>(coc-format-selected)
nmap <leader>f  <Plug>(coc-format-selected)

" Use `[c` and `]c` to navigate diagnostics
nmap <silent> [g <Plug>(coc-diagnostic-prev)
nmap <silent> ]g <Plug>(coc-diagnostic-next)

" GoTo code navigation.
nmap <silent> gd <Plug>(coc-definition)
nmap <silent> gy <Plug>(coc-type-definition)
nmap <silent> gi <Plug>(coc-implementation)
nmap <silent> gr <Plug>(coc-references)

" Use U to show documentation in preview window
nnoremap <silent> U :call <SID>show_documentation()<CR>

" Applying codeAction to the selected region.
" Example: `<leader>aap` for current paragraph
xmap <leader>a  <Plug>(coc-codeaction-selected)
nmap <leader>a  <Plug>(coc-codeaction-selected)

" Remap keys for applying codeAction to the current buffer.
nmap <leader>ac  <Plug>(coc-codeaction)
" Apply AutoFix to problem on the current line.
nmap <leader>qf  <Plug>(coc-fix-current)

" Map function and class text objects
" NOTE: Requires 'textDocument.documentSymbol' support from the language server.
xmap if <Plug>(coc-funcobj-i)
omap if <Plug>(coc-funcobj-i)
xmap af <Plug>(coc-funcobj-a)
omap af <Plug>(coc-funcobj-a)
xmap ic <Plug>(coc-classobj-i)
omap ic <Plug>(coc-classobj-i)
xmap ac <Plug>(coc-classobj-a)
omap ac <Plug>(coc-classobj-a)

" Remap <C-f> and <C-b> for scroll float windows/popups.
nnoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? coc#float#scroll(1) : "\<C-f>"
nnoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? coc#float#scroll(0) : "\<C-b>"
inoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? "\<c-r>=coc#float#scroll(1)\<cr>" : "\<Right>"
inoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? "\<c-r>=coc#float#scroll(0)\<cr>" : "\<Left>"
vnoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? coc#float#scroll(1) : "\<C-f>"
vnoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? coc#float#scroll(0) : "\<C-b>"

" Use CTRL-S for selections ranges.
" Requires 'textDocument/selectionRange' support of language server.
nmap <silent> <C-s> <Plug>(coc-range-select)
xmap <silent> <C-s> <Plug>(coc-range-select)

" Add `:Format` command to format current buffer.
command! -nargs=0 Format :call CocAction('format')

" Add `:Fold` command to fold current buffer.
command! -nargs=? Fold :call CocAction('fold', <f-args>)

" Add `:OR` command for organize imports of the current buffer.
command! -nargs=0 OR :call CocAction('runCommand', 'editor.action.organizeImport')

" Add (Neo)Vim's native statusline support.
" NOTE: Please see `:h coc-status` for integrations with external plugins that
" provide custom statusline: lightline.vim, vim-airline.
set statusline^=%{coc#status()}%{get(b:,'coc_current_function','')}

" Mappings for CoCList
" Show all diagnostics.
nnoremap <silent><nowait> <space>a  :<C-u>CocFzfList diagnostics<cr>
" Manage extensions.
nnoremap <silent><nowait> <space>e  :<C-u>CocFzfList extensions<cr>
" Show commands.
nnoremap <silent><nowait> <space>c  :<C-u>CocFzfList commands<cr>
" Find symbol of current document.
nnoremap <silent><nowait> <space>o  :<C-u>CocFzfList outline<cr>
" Search workspace symbols.
nnoremap <silent><nowait> <space>s  :<C-u>CocFzfList -I symbols<cr>
" Do default action for next item.
nnoremap <silent><nowait> <space>j  :<C-u>CocNext<CR>
" Do default action for previous item.
nnoremap <silent><nowait> <space>k  :<C-u>CocPrev<CR>
" Resume latest coc list.
nnoremap <silent><nowait> <space>p  :<C-u>CocFzfListResume<CR>
" Get a list of yanks and paste
nnoremap <silent> <leader>y  :<C-u>CocList -A --normal yank<cr>

" Python ---------------------------
let g:python_host_prog = "~/.asdf/installs/python/2.7.18/bin/python"
let g:python3_host_prog = "~/.asdf/installs/python/3.10.0/bin/python"

" Golang ---------------------------
" disable vim-go :GoDef short cut (gd)
" this is handled by LanguageClient [LC]
let g:go_def_mapping_enabled = 0

" enable all colour highlighting
let g:go_highlight_build_constraints = 1
let g:go_highlight_extra_types = 1
let g:go_highlight_fields = 1
let g:go_highlight_functions = 1
let g:go_highlight_methods = 1
let g:go_highlight_operators = 1
let g:go_highlight_structs = 1
let g:go_highlight_types = 1

" Highlight all matched words when cursor over
let g:go_auto_sameids = 1

" Enable auto imports
let g:go_fmt_command = "goimports"

" Enable auto tags on structs
let g:go_addtags_transform = "snakecase"

" Show variable types
let g:go_auto_type_info = 1

" Mappings
au FileType go nmap <F9> :GoCoverageToggle -short<cr>
au FileType go nmap <F10> :GoTest -short<cr>
au FileType go nmap <leader>a :GoAlternate<cr>
au FileType go nmap <leader>gt :GoDeclsDir<cr>
au FileType go nmap <leader>gd <Plug>(go-def)
au FileType go nmap <leader>gc <Plug>(go-coverage-toggle)

" Tagbar =========================================
nnoremap <F3> :TagbarToggle<cr>

" Language: Go
" Tagbar configuration for Golang
let g:tagbar_type_go = {
    \ 'ctagstype' : 'go',
    \ 'kinds'     : [
        \ 'p:package',
        \ 'i:imports:1',
        \ 'c:constants',
        \ 'v:variables',
        \ 't:types',
        \ 'n:interfaces',
        \ 'w:fields',
        \ 'e:embedded',
        \ 'm:methods',
        \ 'r:constructor',
        \ 'f:functions'
    \ ],
    \ 'sro' : '.',
    \ 'kind2scope' : {
        \ 't' : 'ctype',
        \ 'n' : 'ntype'
    \ },
    \ 'scope2kind' : {
        \ 'ctype' : 't',
        \ 'ntype' : 'n'
    \ },
    \ 'ctagsbin'  : 'gotags',
    \ 'ctagsargs' : '-sort -silent'
    \ }

" ============================================================================
" Syntax highlighting and linting

" Ale -------------------------------
let g:ale_disable_lsp = 1
let g:ale_open_list = 1
let g:airline#extensions#ale#enabled = 1

let g:ale_fixers = {
      \   '*': ['remove_trailing_lines', 'trim_whitespace'],
      \   'bash': ['shfmt'],
      \   'css': ['prettier'],
      \   'elixir': ['mix_format'],
      \   'haml': ['haml-lint'],
      \   'html': ['prettier'],
      \   'javascript': ['eslint'],
      \   'json': ['prettier'],
      \   'lua': ['luafmt'],
      \   'markdown': ['prettier'],
      \   'ruby': ['rubocop'],
      \   'scss': ['prettier'],
      \   'sh': ['shfmt'],
      \   'sql': ['pgformatter'],
      \   'terraform': ['terraform'],
      \   'yaml': ['prettier']
      \}

augroup CloseLoclistWindowGroup
  autocmd!
  autocmd QuitPre * if empty(&buftype) | lclose | endif
augroup END

" Fugitive --------------------------
set statusline+=%{FugitiveStatusline()}

" Terraform -------------------------
let g:terraform_align=0
let g:terraform_completion_keys = 1
autocmd FileType terraform setlocal commentstring=//%s

" Ansible ---------------------------
" let g:ansible_attribute_highlight = "ob"
let g:ansible_unindent_after_newline = 1
let g:ansible_extra_keywords_highlight = 1

" File extension --------------------
" Language-specific formatting
autocmd FileType go         setlocal autoindent noexpandtab tabstop=4 shiftwidth=4
autocmd FileType py         setlocal autoindent expandtab   tabstop=4 shiftwidth=4
autocmd FileType rb         setlocal autoindent expandtab   tabstop=2 shiftwidth=2
autocmd FileType sh         setlocal autoindent expandtab   tabstop=2 shiftwidth=2
autocmd FileType zsh        setlocal autoindent expandtab   tabstop=2 shiftwidth=2
autocmd FileType make       setlocal autoindent noexpandtab tabstop=2 shiftwidth=2
autocmd FileType yaml       setlocal autoindent expandtab   tabstop=2 shiftwidth=2
autocmd FileType groovy     setlocal autoindent expandtab   tabstop=2 shiftwidth=2
autocmd FileType dockerfile setlocal autoindent expandtab   tabstop=4 shiftwidth=4

" File type formatting
au BufRead,BufNewFile *.tf         setlocal filetype=terraform
au BufRead,BufNewFile *.tfvars     setlocal filetype=terraform
au BufRead,BufNewFile *.parameters setlocal filetype=json
au BufRead,BufNewFile *.tags       setlocal filetype=json
au BufRead,BufNewFile *.template   setlocal filetype=yaml
au BufRead,BufNewFile Dockerfile.* setlocal filetype=dockerfile
au BufRead,BufNewFile Jenkinsfile  setlocal filetype=groovy
au BufRead,BufNewFile *makefile*   setlocal filetype=make
